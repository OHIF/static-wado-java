plugins {
    id 'org.dcm4che.staticwado.java-application-conventions'
}

configurations {
    jniMac
    jniLinux
    jniWindows
}

dependencies {
    implementation 'org.apache.commons:commons-text'
    implementation 'commons-cli:commons-cli:1.4'
    implementation project(':StudyManager')
    implementation 'org.slf4j:slf4j-simple:1.7.32'
    jniLinux 'org.weasis.thirdparty.org.opencv:libopencv_java:4.5.3-dcm:linux-x86-64@so'
    jniMac 'org.weasis.thirdparty.org.opencv:libopencv_java:4.5.3-dcm:macosx-x86-64@jnilib'
    jniWindows 'org.weasis.thirdparty.org.opencv:opencv_java:4.5.3-dcm:windows-x86-64@dll'
    implementation 'org.dcm4che:dcm4che-core:5.25.0'
    implementation 'org.dcm4che:dcm4che-imageio:5.25.0'
    implementation 'org.dcm4che:dcm4che-net:5.25.0'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.39'
}

application {
    applicationName = "StaticWado"
    // Define the main class for the application.
    mainClass = 'org.dcm4che.staticwado.StaticWado'
}

tasks.register('copyJniLinux', Copy) {
    from project.configurations.jniLinux
    into "${project.buildDir}/jni/Linux-64"
    rename '([^-]*)-(.*)', '$1.so'
}

tasks.register('copyJniMac', Copy) {
    from project.configurations.jniMac
    into "${project.buildDir}/jni/Mac"
    rename '([^-]*)-(.*)', '$1.jnilib'
}

tasks.register('copyJniWindows', Copy) {
    from project.configurations.jniWindows
    into "${project.buildDir}/jni/Windows-x86-64"
    rename '([^-]*)-(.*)', '$1.dll'
}

tasks.compileJava.dependsOn(tasks.copyJniWindows, tasks.copyJniMac, tasks.copyJniLinux)

task scpStartScripts(type: CreateStartScripts) {
    outputDir = file("build/scripts") // By putting these scripts here, they will be picked up automatically by the installDist task
    mainClass = "org.dcm4che.staticwado.StaticWadoScp"
    applicationName = 'StaticWadoScp'
    classpath = project.tasks.getAt(JavaPlugin.JAR_TASK_NAME).outputs.files.plus(project.configurations.getByName(JavaPlugin.RUNTIME_CLASSPATH_CONFIGURATION_NAME))
}

task outStartScripts(type: CreateStartScripts) {
    outputDir = file("build/scripts") // By putting these scripts here, they will be picked up automatically by the installDist task
    mainClass = "org.dcm4che.staticwado.StaticWadoOut"
    applicationName = 'StaticWadoOut'
    classpath = project.tasks.getAt(JavaPlugin.JAR_TASK_NAME).outputs.files.plus(project.configurations.getByName(JavaPlugin.RUNTIME_CLASSPATH_CONFIGURATION_NAME))
}

tasks.named("installDist") {
    dependsOn(scpStartScripts)
    dependsOn(outStartScripts)
}

distributions {
    main {
        contents {
            from {
               files("${project.buildDir}/jni")
            }
        }
    }
}
