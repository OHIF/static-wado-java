plugins {
    id 'org.dcm4che.staticwado.java-application-conventions'
}

configurations {
    jniMac
    jniLinux
    jniWindows
}

dependencies {
    implementation 'org.apache.commons:commons-text'
    implementation 'org.slf4j:slf4j-simple:1.7.32'
    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    implementation 'commons-cli:commons-cli:1.4'
    implementation project(':StudyManager')
    jniLinux 'org.weasis.thirdparty.org.opencv:libopencv_java:4.5.3-dcm:linux-x86-64@so'
    jniMac 'org.weasis.thirdparty.org.opencv:libopencv_java:4.5.3-dcm:macosx-x86-64@jnilib'
    jniWindows 'org.weasis.thirdparty.org.opencv:opencv_java:4.5.3-dcm:windows-x86-64@dll'
    implementation 'org.dcm4che:dcm4che-core:5.24.1'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.39'
}

application {
    // Define the main class for the application.
    mainClass = 'org.dcm4che.staticwado.StaticWado'
}

tasks.register('copyJniLinux', Copy) {
    from project.configurations.jniLinux
    into "${project.buildDir}/jni/Linux-64"
    rename '([^-]*)-(.*)', '$1.so'
}

tasks.register('copyJniMac', Copy) {
    from project.configurations.jniMac
    into "${project.buildDir}/jni/Mac"
    rename '([^-]*)-(.*)', '$1.jnilib'
}

tasks.register('copyJniWindows', Copy) {
    from project.configurations.jniWindows
    into "${project.buildDir}/jni/Windows-x86-64"
    rename '([^-]*)-(.*)', '$1.dll'
}

tasks.compileJava.dependsOn(tasks.copyJniWindows, tasks.copyJniMac, tasks.copyJniLinux)


distributions {
    main {
        contents {
            from {
               files("${project.buildDir}/jni")
            }
        }
    }
}